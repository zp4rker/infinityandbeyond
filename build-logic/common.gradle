apply plugin: "java"

def pluginName = "InfinityAndBeyond-${project.name.capitalize()}"
def pluginPrefix = "IAB-${project.name.capitalize()}"
def apiVersion = rootProject.property("apiVersion")
def spigotVersion = "${apiVersion}-R0.1-SNAPSHOT"

repositories {
    mavenCentral()

    maven {
        url = uri("https://repo.codemc.io/repository/nms/") // For spigot
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:${spigotVersion}")
}

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.compilerArgs << "-parameters"
compileJava.options.compilerArgs << "-Xlint:-options"

tasks {
    jar {
        archiveFileName = "${rootProject.name}-${project.name}-${project.version}.jar"
    }
}

processResources {
    def props = [
            pluginName  : pluginName,
            version     : project.version,
            description : "${rootProject.description} [${project.name}]",
            pluginPrefix: pluginPrefix,
            apiVersion  : apiVersion
    ]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}